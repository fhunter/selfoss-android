buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:0.13.2'
        classpath 'com.neenbedankt.gradle.plugins:android-apt:1.4'
    }
}
apply plugin: 'com.android.application'
apply plugin: 'android-apt'

repositories {
    mavenCentral()
}

dependencies {
    compile 'com.android.support:support-v4:20.0.0'
    compile 'com.android.support:support-v13:20.0.0'
	compile 'org.apache.commons:commons-lang3:3.1'
	compile 'org.springframework.android:spring-android-rest-template:1.0.1.RELEASE'
    compile 'com.fasterxml.jackson.core:jackson-databind:2.2.2'
	compile 'com.j256.ormlite:ormlite-android:4.45'
    compile 'com.github.chrisbanes.actionbarpulltorefresh:library:0.9.3'
    compile 'org.jsoup:jsoup:1.7.3'
    compile 'com.nostra13.universalimageloader:universal-image-loader:1.9.3'

    apt 'org.androidannotations:androidannotations:3.1'
    compile 'org.androidannotations:androidannotations-api:3.1'
}

android {
    compileSdkVersion 21
    buildToolsVersion "21.1"

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 21
        versionCode 27
        versionName "0.8.3"
    }

    signingConfigs {
        release {
            storeFile file(project.rootDir.parentFile.absolutePath+"/yDelouis.keystore")
            storePassword "***********"
            keyAlias "ydelouis"
            keyPassword "***********"
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
        }
    }

    packagingOptions {
        pickFirst 'META-INF/DEPENDENCIES.txt'
        pickFirst 'META-INF/LICENSE.txt'
        pickFirst 'META-INF/NOTICE.txt'
        pickFirst 'META-INF/NOTICE'
        pickFirst 'META-INF/LICENSE'
        pickFirst 'META-INF/DEPENDENCIES'
        pickFirst 'META-INF/notice.txt'
        pickFirst 'META-INF/license.txt'
        pickFirst 'META-INF/dependencies.txt'
        pickFirst 'META-INF/LGPL2.1'
    }
}

apt {
    arguments {
        logLevel 'warn'
        androidManifestFile variant.processResources.manifestFile
        resourcePackageName 'fr.ydelouis.selfoss'
    }
}

task askForPasswords << {
    def storePw = new String(System.console().readPassword("\n\nKeystore password: "))
    def keyPw = new String(System.console().readPassword("Key password: "))

    android.signingConfigs.release.storePassword = storePw
    android.signingConfigs.release.keyPassword = keyPw
}

tasks.whenTaskAdded { theTask ->
    if (theTask.name.equals("packageRelease")) {
        theTask.dependsOn "askForPasswords"
    }
}

task launchDebug(dependsOn: 'installDebug') << {
    "adb shell am start -n fr.ydelouis.selfoss/.activity.MainActivity_".execute()
}

task clearData << {
    "adb shell pm clear fr.ydelouis.selfoss".execute()
}
